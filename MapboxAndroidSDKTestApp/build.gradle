//apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

repositories {
    mavenCentral()
    mavenLocal()
    flatDir {
        dirs '../libs'
    }
}

android {
    compileSdkVersion 25
	buildToolsVersion '25.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        applicationId "com.mapbox.mapboxsdk.android.testapp"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // This is important, it will run lint checks but won't abort build
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile(project(':MapboxAndroidSDK')) {
        transitive = true
    }
    compile 'com.android.support:support-annotations:25.0.1'
	compile ('com.android.support:support-compat:25.0.1') { force = true }		// really use the specified even if something else resolves higher
	compile ('com.android.support:support-core-utils:25.0.1') { force = true }	// really use the specified even if something else resolves higher
    compile 'com.android.support:design:25.0.1'
}


checkstyle {
    configFile project.file('../checks.xml')
    showViolations true
}

android.applicationVariants.all { variant ->
    def name = variant.buildType.name
    def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
    checkstyle.dependsOn variant.javaCompile
    checkstyle.source variant.javaCompile.source
    checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
    checkstyle.exclude('**/BuildConfig.java')
    checkstyle.exclude('**/R.java')
    project.tasks.getByName("check").dependsOn checkstyle
}
